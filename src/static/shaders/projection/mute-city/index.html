<!DOCTYPE html>
<html>

<head>
  <link rel="apple-touch-icon" sizes="57x57" href="/favicon/apple-icon-57x57.png">
  <link rel="apple-touch-icon" sizes="60x60" href="/favicon/apple-icon-60x60.png">
  <link rel="apple-touch-icon" sizes="72x72" href="/favicon/apple-icon-72x72.png">
  <link rel="apple-touch-icon" sizes="76x76" href="/favicon/apple-icon-76x76.png">
  <link rel="apple-touch-icon" sizes="114x114" href="/favicon/apple-icon-114x114.png">
  <link rel="apple-touch-icon" sizes="120x120" href="/favicon/apple-icon-120x120.png">
  <link rel="apple-touch-icon" sizes="144x144" href="/favicon/apple-icon-144x144.png">
  <link rel="apple-touch-icon" sizes="152x152" href="/favicon/apple-icon-152x152.png">
  <link rel="apple-touch-icon" sizes="180x180" href="/favicon/apple-icon-180x180.png">
  <link rel="icon" type="image/png" sizes="192x192" href="/favicon/android-icon-192x192.png">
  <link rel="icon" type="image/png" sizes="32x32" href="/favicon/favicon-32x32.png">
  <link rel="icon" type="image/png" sizes="96x96" href="/favicon/favicon-96x96.png">
  <link rel="icon" type="image/png" sizes="16x16" href="/favicon/favicon-16x16.png">
  <link rel="manifest" href="/favicon/manifest.json">
  <meta name="msapplication-TileColor" content="#ffffff">
  <meta name="msapplication-TileImage" content="/favicon/ms-icon-144x144.png">
  <meta name="theme-color" content="#ffffff">
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <meta charset="UTF-8">
  <link rel="stylesheet" type="text/css" href="/shared.css">
  <title>Mute City - SUNRUSE</title>
</head>

<body>
  <nav>
    <a href="/">
      <h1>
        <img src="/logo.svg" onerror="this.src='/logo.png'" alt="SUNRUSE">
      </h1>
    </a>
    <h2>Open Source</h2>
    <a href="/game-prototypes">Game Prototypes</a>
    <a href="/shaders">Shaders</a>
    <h2>Miscellaneous</h2>
    <a href="/miscellaneous/3d-modelling">3D Modelling</a>
    <a href="/miscellaneous/about-contact">About/Contact</a>
    <a href="/miscellaneous/links">Links</a>
  </nav>
  <link rel="stylesheet" type="text/css" href="../../viewer.css">
  <div id="loading">
    <img src="preview.png" />
    <div id="loading-description">Downloading...</div>
  </div>
  <script id="code" type="glsl">
#ifdef GL_ES
    precision mediump float;
#endif
uniform float seconds;
uniform vec2 resolution;
uniform vec2 mouse;
#define PI 3.14159265359
#define TriplePI (3.0 * PI)
#define DoublePI (2.0 * PI)
#define HalfPI (PI / 2.0)
/* 
   These functions take a "camera" variable specifying:
     X/Y: Where on the screen we are rendering.
     Z: How far forwards we are.
   And return the world-space coordinate at which the camera hits the plane.
*/
/* A wall directly in front of you. */
vec3 planeZ(float z, vec3 camera) {
    return vec3(camera.xy * (z - camera.z), z);
}
/* Parallel walls to the left/right of you. */
vec3 planeX(float x, vec3 camera) {
    float divisor = x / camera.x;
    return vec3(x, camera.y * divisor, abs(divisor) + camera.z);
}       
/* Parallel floor and ceiling. */
vec3 planeY(float y, vec3 camera) {
    float divisor = y / -camera.y;
    return vec3(camera.x * -divisor, y, abs(divisor) + camera.z);
}
/* -------------------------------------------------------- */
void main()
{
    // Almost normalized device coordinates, but aspect-corrected.
    vec2 aspectNdc = (gl_FragCoord.xy - (resolution / 2.0)) / (min(resolution.x, resolution.y) / 2.0);
    // Distortion which sort of looks like rotating the camera.
    vec2 mouse_ = (mouse - (resolution / 2.0)) / min(resolution.x, resolution.y);
    mouse_ *= 2.0;
    mouse_.y += 0.3;
    aspectNdc -= mouse_.xy;
    aspectNdc /= 1.0 + dot(mouse_.xy, aspectNdc);
    vec3 camera = vec3(aspectNdc, seconds);
    vec3 col;
    if(camera.y > 0.0) {
        if(camera.y > 0.0 && camera.y < 0.18 - abs(sin(camera.x * 1.2 + 1.4) * 0.1) - abs(sin(camera.x * 3.0) * 0.05)) 
            // Mountain.
            col = vec3(0.0, 0.0, 1.0);
        else
            // Sky.
            col = vec3(0.3, 0.7, 1.0);
        // Fade to white on horizon.
        col = mix(col, vec3(0.8, 0.9, 1.0), 1.0 / (1.0 + camera.y * 4.0));
    } else {
        // Houses/streets.
        #define GROUND_ALTITUDE -4.3
        #define ROOF_ALTITUDE -4.0
        #define BUILDING_SIZE 0.6
        #define ROAD_WIDTH (BLOCK_SIZE - BUILDING_SIZE)
        #define BLOCK_SIZE 1.0
        vec3 roof = planeY(ROOF_ALTITUDE, camera);
        vec2 tile = floor(roof.xz / BLOCK_SIZE) * BLOCK_SIZE;
        float leftX = tile.x + ROAD_WIDTH / 2.0;
        float rightX = tile.x + BUILDING_SIZE + ROAD_WIDTH / 2.0;
        float frontZ = tile.y + ROAD_WIDTH / 2.0;
        float backZ = tile.y + BUILDING_SIZE + ROAD_WIDTH / 2.0;
        float sideX = roof.x > 0.0 ? leftX : rightX;
        vec3 front = planeZ(frontZ, camera);
        vec3 side = planeX(sideX, camera);
        vec3 ground = planeY(GROUND_ALTITUDE, camera);
        vec3 compute;
        col = vec3(0.12, 0.09, 0.04);
        float brightness;
        if(roof.x > leftX && roof.x < rightX && roof.z > frontZ && roof.z < backZ) {
            compute = roof;
            brightness = 0.0;
        } else {
            if(front.x > leftX 
               && front.x < rightX 
               && front.y > GROUND_ALTITUDE 
               && front.y < ROOF_ALTITUDE
            ) {
                compute = front;
                brightness = 0.4;  
            } else {
                if(side.z > frontZ 
                   && side.z < backZ 
                   && side.y > GROUND_ALTITUDE 
                   && side.y < ROOF_ALTITUDE) {
                    compute = side;
                    brightness = 0.7;
                } else {
                    compute = ground;
                    brightness = 1.0;
                }
            }
        }
        // Apply streetlights.
        vec2 groundUV = cos(compute.xz * PI / BLOCK_SIZE * 2.0) * 0.5 + 0.5;
        col += vec3(0.5, 0.7, 1.3) * brightness * ((groundUV.x * groundUV.y + 0.01) / (((compute.y - GROUND_ALTITUDE)* 10.0) + 1.0));
        #define OVERPASS_ALTITUDE -3.5
        #define OVERPASS_2_ALTITUDE -3.0
        #define OVERPASS_SPACING 12.0
        #define OVERPASS_2_SPACING 7.0
        #define OVERPASS_WIDTH 0.125
        // Pink overpass.
        vec3 overpass = planeY(OVERPASS_ALTITUDE, camera);
        float overpassY = 
            min(abs(mod(overpass.z, OVERPASS_SPACING) - OVERPASS_WIDTH),
                abs(mod(overpass.x, OVERPASS_SPACING) - OVERPASS_WIDTH))
                / OVERPASS_WIDTH;
        if(overpassY < 1.0) {
            col = mix(vec3(0.5, 0.1, 0.4), vec3(1.0), (overpassY * overpassY) + sin(((overpass.x + overpass.z) * 5.0) + seconds * 30.0) * 0.2);
            compute = overpass;
        }
        // Blue overpass.
        overpass = planeY(OVERPASS_2_ALTITUDE, camera);
        overpass.xy += 12.0;
        overpassY = 
            min(abs(mod(overpass.z, OVERPASS_2_SPACING) - OVERPASS_WIDTH), 
                abs(mod(overpass.x, OVERPASS_2_SPACING) - OVERPASS_WIDTH))
                / OVERPASS_WIDTH;
        if(overpassY < 1.0) {
            col = mix(vec3(0.2, 0.5, 1.0), vec3(1.0), (overpassY * overpassY) + sin(((overpass.x + overpass.z) * -8.0) + seconds * 30.0) * 0.2);
            compute = overpass;
        }
        // Fog.
        col = mix(vec3(1.0, 0.6, 0.7), col, 1.0 / (1.0 + (compute.z - camera.z) * 0.03));
        col = mix(vec3(0.8, 0.9, 1.0), col, 1.0 / (1.0 + (compute.z - camera.z) * 0.005));
    }
    // Gamma correct and put on screen.
    gl_FragColor = vec4(pow(col, vec3(1.0 / 2.2)), 1.0);
}
</script>
  <div id="toolbar">
    <a href="../../">Back</a>
    <input id="play" name="play" type="checkbox" checked>
    <label for="play">Play</label>
    <input id="show-code" name="show-code" type="checkbox">
    <label for="show-code">Show Code</label>
  </div>
  <div id="canvas-container">
    <canvas></canvas>
  </div>
  <script src="../../viewer.js"></script>
</body>

</html>